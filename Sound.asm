;----------------------------------------------------------------------------
;  Функции генерации звука через PC Speaker
;
;  Прототипы:
;    void __fastcall Sound(int Frequency);
;    void __fastcall NoSound(void);
;
;  Для подключения к модулю C++ функции нужно объявить как 'extern "C"'
;----------------------------------------------------------------------------
; 
;  Для компиляции используйте следующую команду:
;    tasm32 /z /ml Sound.asm
;  Полученный OBJ файл можно подсоединять к программе.
;
;  Примечание: можно также просто добавить этот файл к проекту, если
;    Ваш компилятор разрешает это сделать.
;
;----------------------------------------------------------------------------
.386                            ; Требуется для 32 битных сегментов
MODEL FLAT                      ; Модель памяти 32 битных программ

PUBLIC @Sound@4, @NoSound@0     ; Внешние символы (экпортируемые)

.CODE                           ; Начало сегмента кода

@Sound@4 PROC
;
;  Включает PC Speaker на вывод выбранной частоты
;  На входе ECX содержит частоту (используется только младшее слово)
;
      ; Вычисляем делитель частоты таймера, чтобы звук был с
      ;   заданной частотой
      mov     ax, 34DDh   ; DX:AX - маскимальный делитель частоты
      mov     dx, 0012h
      cmp     dx, cx      ; Проверяем, чтобы не было ошибки при делении
      jnb     @@S20       ; Выход (при делении возникнет ошибка)
      div     cx          ; AX := DX:AX div CX
      mov     cx, ax      ; AX - нужный делитель
      in      al, 61h     ; Получяем состояние порта динамика
      test    al, 03h     ; Проверяем, включен ли динамик
      jnz     @@S10       ; Если да, то переходим к @@S10
      or      al , 03h    ; Включяем динамик
      out     61h, al     ; Выводим новое значение в порт
@@S10:
      mov     al , 0B6h   ; Выбираем перепрограммируемый генератор частоты
                          ;   и устанавливаем его параметры
      out     43h, al     ; Помещаем значение в управляющий порт
      mov     al , cl     ; Теперь заносим в порт данных делитель частоты
      out     42h, al
      mov     al , ch
      out     42h, al
@@S20:
      ret
ENDP                      ; Конец @Sound

@NoSound@0 PROC
;
; Выключает PC Speaker
;
      in      al,61h      ; Получяем состояние порта
      and     al,0FCh     ; Сбрасываем биты динамика
      out     61h,al      ; Помещаем новое значение в порт
      ret
ENDP                      ; Конец @NoSound

END                       ; Конец файла